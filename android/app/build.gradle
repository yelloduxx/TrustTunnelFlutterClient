plugins {
    id "com.android.application"
    id "kotlin-android"
    // The Flutter Gradle Plugin must be applied after the Android and Kotlin Gradle plugins.
    id "dev.flutter.flutter-gradle-plugin"
}

allprojects {
    repositories {
        google()
        mavenCentral()
        mavenLocal()
    }
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file("local.properties")
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader("UTF-8") { reader ->
        localProperties.load(reader)
    }
}

def flutterVersionCode = localProperties.getProperty("flutter.versionCode")
if (flutterVersionCode == null) {
    flutterVersionCode = "1"
}

def flutterVersionName = localProperties.getProperty("flutter.versionName")
if (flutterVersionName == null) {
    flutterVersionName = "1.0"
}


def getSigningProp = { String name ->
    def v = project.findProperty(name)
    if (v != null) return v.toString()
    v = localProperties.getProperty(name)
    return v != null ? v.toString() : null
}

def signingKeyAlias = getSigningProp("signingConfigKeyAlias")
def signingKeyPassword = getSigningProp("signingConfigKeyPassword")
def signingStorePath = getSigningProp("signingConfigKeyStorePath")
def signingStorePassword = getSigningProp("signingConfigKeyStorePassword")
def hasReleaseSigningConfig = (
    signingKeyAlias != null &&
    signingKeyPassword != null &&
    signingStorePath != null &&
    signingStorePassword != null
)

android {
    namespace = "com.adguard.trusttunnel"
    compileSdk = flutter.compileSdkVersion
    ndkVersion = "28.1.13356709"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = "17"
    }

    defaultConfig {
        // TODO: Specify your own unique Application ID (https://developer.android.com/studio/build/application-id.html).
        applicationId = "com.adguard.trusttunnel"
        // You can update the following values to match your application needs.
        // For more information, see: https://docs.flutter.dev/deployment/android#reviewing-the-gradle-build-configuration.
        minSdk = 26
        targetSdk = 35
        versionCode = flutterVersionCode.toInteger()
        versionName = flutterVersionName
    }

    signingConfigs {
        release {
            if (hasReleaseSigningConfig) {
                keyAlias = signingKeyAlias
                keyPassword = signingKeyPassword
                storeFile = rootProject.file(signingStorePath)
                storePassword = signingStorePassword
            }
        }
    }

    buildTypes {
        release {
            signingConfig hasReleaseSigningConfig ? signingConfigs.release : signingConfigs.debug
        }
    }
}

flutter {
    source = "../.."
}
